--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: SettingColumnNumberInPreference
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _XxFF4JQgEeWNpL9oeriwCw
Runtime-Version: 2.2.0.201706152316
Save-Time: 6/29/18 5:34 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "check-column-amount-for-main_view" [val view] [val tab_label] [val col_nr] [val row_nr 0]
{
	get-editor "TestFile.robot" | get-tab-folder | get-tab-item $view | click
	
	with [get-editor "TestFile.robot" | get-canvas -after [get-label $tab_label] -index 1 ] {
	    get-property "getLayer().getBodyLayer().getScrollableLayer().getColumnCount()" | equals $col_nr | verify-true
	    get-property "getLayer().getBodyLayer().getScrollableLayer().getRowCount()"  | equals $row_nr | verify-true
	}
}

proc "check-column-amount-in-settings" [val tab_label] [val col_nr] 
{

	with [get-editor "TestFile.robot" | get-section $tab_label | get-canvas] {
	    get-property "getLayer().getBodyLayer().getScrollableLayer().getColumnCount()" | equals $col_nr | verify-true
	    get-property "getLayer().getBodyLayer().getScrollableLayer().getRowCount()"  | equals 0 | verify-true
	}
}


proc "set-correct-column-number-in-preference" [val col_nr]{
	get-window Preferences | get-group Tables | get-editbox 
    -after [get-label "Default number of columns for arguments in table editors"] | set-text $col_nr
    
    //check if error is not shown
    get-window Preferences | get-label Editor | get-property "getText()" | equals Editor | verify-true
}

proc "set-incorrect-column-number-in-preference" [val col_nr]{
	get-window Preferences | get-group Tables | get-editbox 
    -after [get-label "Default number of columns for arguments in table editors"] | set-text $col_nr
    //check error
	get-window Preferences | get-editbox -index 1 | get-property text | equals "Value must be an Integer between 1 and 20" 
	    | verify-true
	//set correct value    
	set-correct-column-number-in-preference -col_nr "8"
}


//---------------------------------------------------------------------------------------------------------------------
//check default preference
get-preferences-menu | click
get-window Preferences | get-tree | select "Robot Framework/Editor"
with [get-window Preferences | get-group Tables] {
    get-property "getChildren().length" | equals 6 | verify-true
    get-property "getChildren().Control[0].getText()" 
        | equals "Default number of columns for arguments in table editors" | verify-true
    get-property "getChildren().Control[1].getText()" | equals 5 | verify-true
    get-property "getChildren().Control[2].getText()" | equals "When cell is too small for content" | verify-true
    get-property "getChildren().Control[3].getText()" | equals "cut the content" | verify-true
    get-property "getChildren().Control[4].getText()" | equals "After pressing Enter in cell under edit" | verify-true
    get-property "getChildren().Control[5].getText()" | equals "move to next cell (previous with Shift pressed)" 
        | verify-true
    with [get-combo -after [get-label "When cell is too small for content"]] {
        get-property "values[0]" | equals "cut the content" | verify-true
        get-property "values[1]" | equals "wrap the content" | verify-true
    }
    with [get-combo -after [get-label "After pressing Enter in cell under edit"]] {
        get-property "values[0]" | equals "stay in the same cell" | verify-true
        get-property "values[1]" | equals "move to next cell (previous with Shift pressed)" | verify-true
    }
}
get-window Preferences | get-button Cancel | click

//open editor and check how many columns are in views
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click

//variables
check-column-amount-for-main_view -view "Variables" -tab_label "Variables" -col_nr 3

//go to settings
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Settings | click
check-column-amount-in-settings -tab_label "General" -col_nr 7

//go to metadata
with [get-editor "TestFile.robot"] {
    get-section General | click
    get-section Metadata | click
}

check-column-amount-in-settings -tab_label "Metadata" -col_nr 3

//go to Imports
get-editor "TestFile.robot" | get-section Imports | click
check-column-amount-in-settings -tab_label "Imports" -col_nr 8

//go to keywords
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Keywords | click
check-column-amount-for-main_view -view "Keywords" -tab_label "User Keywords" -col_nr 6

//go to test cases
check-column-amount-for-main_view -view "Test Cases" -tab_label "Test Cases" -col_nr 6 -row_nr 1


//set incorrect column number
get-preferences-menu | click
set-incorrect-column-number-in-preference -col_nr 0
set-incorrect-column-number-in-preference -col_nr 21
set-incorrect-column-number-in-preference -col_nr "-1"
set-incorrect-column-number-in-preference -col_nr "-150"
set-incorrect-column-number-in-preference -col_nr "te"
set-incorrect-column-number-in-preference -col_nr ",."


//set correct number of column
set-correct-column-number-in-preference -col_nr 1
set-correct-column-number-in-preference -col_nr 5
set-correct-column-number-in-preference -col_nr 12
set-correct-column-number-in-preference -col_nr 20

//set to default
get-window Preferences | get-button "Restore Defaults" | click
get-window Preferences | get-group Tables | get-editbox 
    -after [get-label "Default number of columns for arguments in table editors"] | get-property text | equals 5 
    | verify-true


//--------------------------------------------------------------------------------------------------------------------------    
//set amout of column = 1
with [get-window Preferences] {
    get-group Tables | get-editbox -after [get-label "Default number of columns for arguments in table editors"] 
        | set-text 1
    get-button $Apply_and_OK_in_preference | click
}

//restart view    
get-view "Project Explorer" | click
get-editor "TestFile.robot" | close
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click

//check test cases
check-column-amount-for-main_view -view "Test Cases" -tab_label "Test Cases" -col_nr 2 -row_nr 1
//KW
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Keywords | click
check-column-amount-for-main_view -view "Keywords" -tab_label "User Keywords" -col_nr 2

//settings
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Settings | click
check-column-amount-in-settings -tab_label "General" -col_nr 3

//go to metadata
with [get-editor "TestFile.robot"] {
    get-section General | click
    get-section Metadata | click
}

check-column-amount-in-settings -tab_label "Metadata" -col_nr 3

//go to Imports
get-editor "TestFile.robot" | get-section Imports | click
check-column-amount-in-settings -tab_label "Imports" -col_nr 4

check-column-amount-for-main_view -view "Variables" -tab_label "Variables" -col_nr 3
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Variables | click

//--------------------------------------------------------------------------------------------

//set column number to 20
get-preferences-menu | click
with [get-window Preferences] {
    get-group Tables | get-editbox -after [get-label "Default number of columns for arguments in table editors"] 
        | set-text 20
    get-button $Apply_and_OK_in_preference | click
}
get-view "Project Explorer" | click
get-editor "TestFile.robot" | close
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click



//check test cases
get-editor "TestFile.robot" | get-tab-folder | get-tab-item "Test Cases" | click
check-column-amount-for-main_view -view "Test Cases" -tab_label "Test Cases" -col_nr 21 -row_nr 1

//KW
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Keywords | click
check-column-amount-for-main_view -view "Keywords" -tab_label "User Keywords" -col_nr 21

//settings
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Settings | click
check-column-amount-in-settings -tab_label "General" -col_nr 22

//go to metadata
with [get-editor "TestFile.robot"] {
    get-section General | click
    get-section Metadata | click
}

check-column-amount-in-settings -tab_label "Metadata" -col_nr 3

//go to Imports
get-editor "TestFile.robot" | get-section Imports | click
check-column-amount-in-settings -tab_label "Imports" -col_nr 23

check-column-amount-for-main_view -view "Variables" -tab_label "Variables" -col_nr 3 
get-editor "TestFile.robot" | get-tab-folder | get-tab-item Variables | click

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
